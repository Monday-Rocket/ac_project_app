plugins {
    id "com.android.application"
    id "kotlin-android"
    id "dev.flutter.flutter-gradle-plugin"
    id "kotlin-parcelize"
    id 'com.google.gms.google-services'
    id 'com.google.firebase.crashlytics'
}

def localProperties = new Properties()
def localPropertiesFile = rootProject.file('local.properties')
if (localPropertiesFile.exists()) {
    localPropertiesFile.withReader('UTF-8') { reader ->
        localProperties.load(reader)
    }
}

def flutterVersionCode = localProperties.getProperty('flutter.versionCode')
if (flutterVersionCode == null) {
    flutterVersionCode = 1
}

def flutterVersionName = localProperties.getProperty('flutter.versionName')
if (flutterVersionName == null) {
    flutterVersionName = '1.0'
}

def signingProperties = new Properties()
def signingPropertiesFile = rootProject.file('signing.properties')
if (signingPropertiesFile.exists()) {
    signingPropertiesFile.withReader('UTF-8') {
        signingProperties.load(it)
    }
}
def _keyAlias = signingProperties.getProperty('key.alias')
def _keyPassword = signingProperties.getProperty('key.password')
def _storePassword = signingProperties.getProperty('store.password')
def kakaoApiKey = signingProperties.getProperty('kakao.api.key')

android {
    compileSdkVersion 34

    compileOptions {
        sourceCompatibility = JavaVersion.VERSION_11
        targetCompatibility = JavaVersion.VERSION_11
    }
    kotlinOptions {
        jvmTarget = "11"
    }

    viewBinding {
        enabled = true
    }

    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    defaultConfig {
        applicationId "com.mr.ac_project_app"
        minSdkVersion 24
        targetSdkVersion 34
        versionCode flutterVersionCode.toInteger()
        versionName flutterVersionName
        ndk.debugSymbolLevel 'FULL'
        manifestPlaceholders.KAKAO_API_KEY = kakaoApiKey
    }

    buildFeatures {
        buildConfig = true
    }

    signingConfigs {
        release {
            keyAlias _keyAlias
            keyPassword _keyPassword
            storeFile file('../monday-key.jks')
            storePassword _storePassword
        }
    }

    buildTypes {
        release {
            shrinkResources true
            minifyEnabled true
            signingConfig signingConfigs.release
        }
    }
    namespace 'com.mr.ac_project_app'

}

flutter {
    source '../..'
}

dependencies {
    //noinspection GradleDependency
    implementation 'androidx.appcompat:appcompat:1.4.2'
    implementation 'androidx.activity:activity-ktx:1.6.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.github.bumptech.glide:glide:4.14.2'
    implementation 'org.jsoup:jsoup:1.13.1'
    implementation 'com.jakewharton.threetenabp:threetenabp:1.4.3'
    implementation 'commons-codec:commons-codec:1.11'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.14.2'
}

configurations.all {
    exclude group: 'com.google.android.gms', module: 'play-services-safetynet'
}